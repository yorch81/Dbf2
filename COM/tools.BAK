*********************************************************
* COM Server generates Scripts Files
*
* @package    Dbf2Csv
* @copyright  Copyright 2014 JAPT
* @license    http://www.apache.org/licenses/LICENSE-2.0
* @version    1.0.0, 2014-09-01
* @author     Jorge Alberto Ponce Turrubiates (the.yorch@gmail.com)
* @return     Generate Import Files
*********************************************************
DEFINE CLASS Tools AS CUSTOM OLEPUBLIC
	*********************************************************
    * CSV Path to Save CSVs ans Scripts Generated
    *
    * @var string csvPath Valid Path
    * @access private
    *********************************************************
	csvPath = SPACE(0)
	
	*********************************************************
    * DataBase Type (SQLServer oo MySQL)
	*
    * @var string DBType DataBaseType
    * @access private
    *********************************************************
	DBType = 'SQLServer'
	
    *********************************************************
	* Generates Files to Import.
	* 1.- CSV Data Files (.csv) 
	* 2.- Create Table Script (.tab)
	* 3.- Import Script (.ins)
	* 4.- Extra Scripts only for SQL Server (.ext)
	* @param string usedTable DBF File (Full Path)
	* @return string Error Message
	*********************************************************
    PROCEDURE generateCSV(usedTable AS String)
        SET DATE TO YMD  
       
       	*********************************************************
	    * Error Message
	    *
	    * @var string retValue Error Message if Exists
	    * @access private
	    *********************************************************
        retValue = ''   
        
        * If CSV path is not defined
        IF LEN(THIS.csvPath) = 0 THEN
        	retValue = "The CSV Path does not Exists"
        ELSE
        	TRY
        		*********************************************************
			    * Open DBF Table
			    *
			    * Notes: The DBF File is open in exclusive mode for generate
			    * 		 CSVs Files.
			    *********************************************************
				USE &usedTable 
				
				* Get CSV File
				fileCsv = THIS.csvPath + ALIAS() + '.csv'
				IF FILE(fileCsv) 
					DELETE FILE &fileCsv
				ENDIF 
				
				* Get SQL CREATE TABLE
				fileTab = THIS.csvPath + ALIAS() + '.tab'
				IF FILE(fileTab) 
					DELETE FILE &fileTab 
				ENDIF 
				
				* Get SQL BULK INSERT o  LOAD DATA
				fileIns = THIS.csvPath + ALIAS() + '.ins'
				IF FILE(fileIns) 
					DELETE FILE &fileIns
				ENDIF 
				
				* Extra Scripts to remove ""
				fileExt = THIS.csvPath + ALIAS() + '.ext'
				IF FILE(fileExt) 
					DELETE FILE &fileExt
				ENDIF 
				
				nameFields = ''
				tableFields = ''
				textFile = ''
				
				* Gets Fields
				numFields = AFIELDS(arrFields) 				
				FOR nCount = 1 TO numFields
					IF THIS.DBType = 'SQLServer' THEN
						tableFields = tableFields + arrFields(nCount,1) + ' VARCHAR(MAX) NULL,' + CHR(13)
					ELSE
						tableFields = tableFields + arrFields(nCount,1) + ' VARCHAR(65535) NULL,' + CHR(13)
					ENDIF
						
					nameFields = nameFields + arrFields(nCount,1) + ','
				ENDFOR

				* Remove last ,
				nameFields = SUBSTR(nameFields, 1, LEN(nameFields)-1)
				tableFields = SUBSTR(tableFields, 1, LEN(tableFields)-2)
				
				* Create CSV Data File
				COPY TO &fileCsv FIELDS &nameFields DELIMITED WITH CHARACTER |
				
				* Create Script SQL CREATE TABLE
				IF THIS.DBType = 'SQLServer' THEN
					textFile = 'CREATE TABLE [dbo].' + ALIAS() + '(' + tableFields + ') ON [PRIMARY]'
				ELSE
					textFile = 'CREATE TABLE ' + ALIAS() + '(' + tableFields + ') ENGINE=InnoDB DEFAULT CHARSET=utf8' 
				ENDIF

				* Save File .TAB
				IF FILE(fileTab) 
					gnTabFile = FOPEN(fileTab,12) 
				ELSE 
					gnTabFile = FCREATE(fileTab)
				ENDIF 

				IF gnTabFile < 0 
					retValue = "Could not open File: " + fileTab
				ELSE 
					=FWRITE(gnTabFile, textFile) 
				ENDIF 

				=FFLUSH(gnTabFile)
				=FCLOSE(gnTabFile) 

				* Create BULK INSERT or LOAD DATA Script
				IF THIS.DBType = 'SQLServer' THEN
					textFile = "BULK INSERT dbo." + ALIAS() + " FROM '" + fileCsv + "' WITH (FIELDTERMINATOR = '|')"
				ELSE
					textFile = "LOAD DATA LOCAL INFILE '" + STRTRAN(fileCsv, '\', '/') + "' INTO TABLE " + ALIAS() + " FIELDS TERMINATED BY '|' ENCLOSED BY '" + '"' + "' LINES TERMINATED BY '\n' (" + nameFields + ");"
				ENDIF
				
				* Save File .INS
				IF FILE(fileIns) 
					gnInsFile = FOPEN(fileIns,12) 
				ELSE 
					gnInsFile = FCREATE(fileIns)
				ENDIF 

				IF gnInsFile < 0 
					retValue = "Could not open File: " + fileIns
				ELSE 
					=FWRITE(gnInsFile, textFile) 
				ENDIF 

				=FFLUSH(gnInsFile)
				=FCLOSE(gnInsFile)  
				
				* Generate EXTRA QUERIES (Only SQL Server)
				IF THIS.DBType = 'SQLServer' THEN
					* Save File .EXT
					IF FILE(fileExt) 
						gnExtFile = FOPEN(fileExt,12) 
					ELSE 
						gnExtFile = FCREATE(fileExt)
					ENDIF 
					
					IF gnExtFile < 0 
						retValue = "Could not open File: "  + fileExt
					ELSE 
						FOR nCount = 1 TO numFields
							nameFields = arrFields(nCount,1)
							IF arrFields(nCount,2) = 'C'
								updCommand = 'UPDATE [dbo].' + ALIAS() + ' SET ' + nameFields + '=REPLACE(' + nameFields + ',' + "'" + '"' + "'" + ',' + "'');"
								=FWRITE(gnExtFile, updCommand) 
							ENDIF
						ENDFOR
					ENDIF 

					=FFLUSH(gnExtFile)
					=FCLOSE(gnExtFile)
				ENDIF				
				
				* Close Table
				CLOSE DATABASES ALL
				CLOSE TABLES ALL
			CATCH TO oErr
				retValue = oErr.Message
			FINALLY
			ENDTRY
        ENDIF
        
        RETURN retValue
    ENDPROC
ENDDEFINE